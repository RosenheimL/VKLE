SakuraMenuGreetings : nonoverlap
{
	"Hello."
	"How can I help you?"
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	"\0\s[0]\b[2]\![set,autoscroll,disable]\_q"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer
	
	"\n\n\_q"
	
	"\![*]\q[What's Up?,OnAiTalk]"
	if LastTalk != ""; "  \![*]\q[Can You Repeat that?,OnLastTalk]"
	"\n\n"
	
	"\![*]\q[You're a Vocaloid? What Have You sung?,OnOliMusic]\n"
	"\![*]\q[Do you know any good Learning Resources?,OnOliResources]\n"
	"\![*]\q[Change talk rate,OnTalkRateMenu]\n\n"
	
	"\![*]\q[Nevermind,blank]"	
}

OnLastTalk
{
	LastTalk
}

OnTalkRateMenu : all
{	
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"\0\s[0]\b2\![set,autoscroll,disable]\_q"
	"Current interval: %(_talkrate)\n\n"
	
	"\![*]\q[Off,OnTalkRateChange,0]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600]\n"
	"\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Nevermind,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
}

OnOliResources : all
{
	"\0\s[0]\b[2]\![set,autoscroll,disable]\_q"
	"Oh Yeah! Here Are Some Beginner Resources For Learning Japanese"
	"\nIf you need Resources for learning English then Yuki should be able to help!"
	"\n\n"
	"\![*]Here's a Really Useful Japanese-English Dictionary\n \_a[https://jisho.org]Jisho Dictionary\_a\n"
	"\![*]In Case You Dont have an Easy Way To Type in Japanese, here's an online virtual keyboard\n \_a[https://www.branah.com/japanese]Branah Keyboard\_a\n"
	"\![*]These are a few different Sites where you can find Free Kids books, They're great for learning!\n \_a[https://www.ehonnavi.net/browse_all/]Ehonnavi\_a  \_a[https://tadoku.org/japanese/en/]Tadoku\_a   \_a[https://ehon.alphapolis.co.jp/]Alphapolis\_a"
}

OnOliMusic 
{
	"\0\s[0]\b[2]\![set,autoscroll,disable]\_q"
	"Oh Of Course, Here are a Few Artists That have Made Music with me\n"
	"\n"
	"\_a[https://www.youtube.com/@corasundae]Corasundae\_a\n"
	"\_a[https://www.youtube.com/@Steampianist]Steampianist\_a\n"
	"\_a[https://www.youtube.com/@momocashew]MomoCashew\_a note-Inactive as a Vocaloid Producer\n"
	"\_a[https://www.youtube.com/@veranranran]ヴェーラン(verankuriensu)\_a - Japanese Artist"
	"\n"
	"\![*]\q[戻る,OnKeroMenu]  \![*]\q[やっぱりやめる,blank]"
}